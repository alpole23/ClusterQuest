process EXTRACT_GCF_REPRESENTATIVES {
    tag "$taxon"
    label 'process_low'
    publishDir "${params.outdir}/bigscape_results/${Utils.sanitizeTaxon(params.taxon)}", mode: 'copy'

    input:
    val taxon
    path bigscape_dir
    path "antismash_input/*", stageAs: 'antismash_input/*'
    path tabulation_file

    output:
    path "gcf_representatives.json", emit: gcf_data

    script:
    def taxon_clean = Utils.sanitizeTaxon(params.taxon)
    def tabulation_arg = Utils.optArg('--tabulation', tabulation_file)
    """
    # Cache key. The scripts below are interpolated paths, not declared inputs,
    # so Nextflow would not otherwise notice when they change. Listed explicitly
    # rather than hashing all of scripts/ — see Utils.scriptsHash.
    # scripts-version: ${Utils.scriptsHash(projectDir, ['clustering/extract_gcf_representatives.py', 'utils'])}
    python ${projectDir}/scripts/clustering/extract_gcf_representatives.py ${bigscape_dir} antismash_input gcf_representatives.json --taxon "${taxon_clean}" ${tabulation_arg}
    """
}
